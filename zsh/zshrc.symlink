# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles
export NVM_LAZY=1

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/(git|system|zsh)/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path, completion files and oh-my-zsh files
 for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/oh-my-zsh/*}
 do
   source $file
 done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Set Spaceship ZSH as a prompt
autoload -U promptinit; promptinit
prompt spaceship

# The next line updates PATH for the Google Cloud SDK.
if [ -f '/Users/jolek/Downloads/google-cloud-sdk/path.zsh.inc' ]; then . '/Users/jolek/Downloads/google-cloud-sdk/path.zsh.inc'; fi

# The next line enables shell command completion for gcloud.
if [ -f '/Users/jolek/Downloads/google-cloud-sdk/completion.zsh.inc' ]; then . '/Users/jolek/Downloads/google-cloud-sdk/completion.zsh.inc'; fi
